# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-keyboard
pkgver=2.0
pkgrel=1
pkgdesc='Keyboard indicator for Wingpanel'
arch=('i686' 'x86_64')
url='https://launchpad.net/wingpanel-indicator-keyboard'
license=('GPL3')
groups=('pantheon-loki')
depends=('glib2' 'glibc' 'gtk3'
         'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('cmake' 'vala')
source=("wingpanel-indicator-keyboard::bzr+lp:wingpanel-indicator-keyboard/loki")
sha256sums=('SKIP')

prepare() {
  cd wingpanel-indicator-keyboard

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd wingpanel-indicator-keyboard/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd wingpanel-indicator-keyboard/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
