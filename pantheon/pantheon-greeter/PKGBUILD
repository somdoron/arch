# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=pantheon-greeter-loki
pkgver=r480
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-greeter'
license=('GPL')
groups=('pantheon-loki')
depends=('cairo' 'clutter' 'clutter-gtk' 'cogl' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gtk3' 'libgee' 'libgl' 'libx11' 'lightdm' 'pango'
         'libgranite.so' 'libwingpanel-2.0.so')
makedepends=('bzr' 'cmake' 'libxfixes' 'vala')
provides=('pantheon-greeter')
conflicts=('pantheon-greeter' 'lightdm-pantheon-greeter')
install='pantheon-greeter.install'
source=('pantheon-greeter::bzr+lp:pantheon-greeter/loki'
        'pantheon-greeter-paths.patch')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd pantheon-greeter

  echo "r$(bzr revno)"
}

prepare() {
  cd pantheon-greeter

  patch -Np1 -i ../pantheon-greeter-paths.patch
}

build() {
  cd pantheon-greeter

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd pantheon-greeter/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
